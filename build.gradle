plugins {
  id "idea"
}

def root = rootDir.absoluteFile.path

configure(allprojects) { project ->

  ext {
    vertxVersion = "3.4.1"
    loggerVersion = "1.7.25"
  }

  apply plugin: "java"
  apply plugin: "maven"
  apply plugin: "maven-publish"

  group "daggerok"
  version "0.0.1"

  jar {
    exclude "META-INF/*.MF", "META-INF/*.SF", "META-INF/*.DSA", "META-INF/*.RSA"
    from {
      configurations.compile.collect { file ->
        file.isDirectory() ? file : zipTree(file)
      }
    }
  }

  sourceCompatibility = JavaVersion.VERSION_1_8

  repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
  }

  publishing {
    publications {
      mavenJava(MavenPublication) {
        from components.java
      }
    }
  }

  publishing {
    repositories {
      maven {
        url "$root/localrepo"
      }
    }
  }

  dependencies {
    ["core", "hazelcast"].each { suffix ->
      compile "io.vertx:vertx-$suffix:$vertxVersion"
    }
    compile "org.slf4j:slf4j-api:$loggerVersion",
        "org.slf4j:slf4j-log4j12:$loggerVersion"
    compileOnly "org.projectlombok:lombok:1.16.16"
    testCompile "io.vertx:vertx-unit:$vertxVersion",
        "junit:junit:4+"
  }
}
